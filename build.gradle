/*
 * Distribution Statement
 *
 * This computer software has been developed under sponsorship of the United States Air Force Research Lab. Any further
 * distribution or use by anyone or any data contained therein, unless otherwise specifically provided for,
 * is prohibited without the written approval of AFRL/RQVC-MSTC, 2210 8th Street Bldg 146, Room 218, WPAFB, OH  45433
 *
 * Disclaimer
 *
 * This material was prepared as an account of work sponsored by an agency of the United States Government. Neither
 * the United States Government nor the United States Air Force, nor any of their employees, makes any warranty,
 * express or implied, or assumes any legal liability or responsibility for the accuracy, completeness, or usefulness
 * of any information, apparatus, product, or process disclosed, or represents that its use would not infringe privately
 * owned rights.
 */

ext {
    enclaveRepo = "10.131.7.138:7001"
    enclave = InetAddress.getLocalHost().getHostAddress().startsWith("10.131")
    noVersion = System.getProperty("nv") != null
}

apply from: file('libraries.gradle')
apply from: file("distribution/dist.gradle")

allprojects {
    if (project.name != "examples") {
        apply plugin: "maven"
        apply plugin: "maven-publish"
        apply plugin: "java"

        repositories {
            mavenLocal()
            if (!enclave) {
                mavenCentral()
                maven { url "http://mvn.sorcersoft.com/content/groups/public" }
            } else {
                maven { url "http://$enclaveRepo" }
            }
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                }
            }
            repositories {
                maven {
                    url "http://$enclaveRepo"
                    //			url "http://repo1.maven.org/maven2"
                }
            }
        }
    }

    group = 'org.sorcer'
    version = '5.0.0-SNAPSHOT'
}


task libDist(dependsOn: assemble) {
	description 'Distributes all SORCER libraries to libs/socer/lib, libs/socer/lib-dl and libs/socer/lib-ext'

	File libRoot = mkdir("${project.rootProject.projectDir.path}/lib")
	File sorcerLibDir = mkdir("${project.rootProject.projectDir.path}/lib/sorcer/lib")
	File sorcerDlDir = mkdir("${project.rootProject.projectDir.path}/lib/sorcer/lib-dl")
	File sorcerExtDir = mkdir("${project.rootProject.projectDir.path}/lib/sorcer/lib-ext")

	doLast {
		allprojects.each { project ->
			project.tasks.withType(Jar).each { jar ->
				File target
				if (jar.archiveName.indexOf('dl') > 0 ||
					jar.archiveName.indexOf('ui') > 0) {
					target = sorcerDlDir
				} else if (jar.path.indexOf('boot') > 0) {
					target = libRoot
				} else if (jar.archiveName.startsWith('ssb-') || jar.archiveName.startsWith('webster-')) {
					target = sorcerExtDir
				} else {
					target = sorcerLibDir
				}
				if (file(jar.archivePath).exists()) {
					copy {
						from jar.archivePath
						into target
					}
				} else {
					println "Archive does not exist ${jar.archivePath}"
				}
			}
		}

	}
}
